autoload -Uz promptinit
autoload -Uz compinit
autoload -Uz vcs_info

HISTFILE=~/.zsh_history
HISTSIZE=500000
SAVEHIST=1000000

stty start '^-' stop '^-'
setopt interactivecomments appendhistory autocd extendedglob nomatch
unsetopt beep

# {{{ vcs_info
precmd() {
  vcs_info
}

+vi-git-untracked() {
  if [[ $(git rev-parse --is-inside-work-tree 2>/dev/null) == 'true' ]] && \
      git status --porcelain | grep -q -m 1 '??'; then
    hook_com[misc]=' %F{9}!%f'
  fi
}

zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
zstyle ':vcs_info:git:*' check-for-changes true
zstyle ':vcs_info:git:*' stagedstr ' %F{10}+%f'
zstyle ':vcs_info:git:*' unstagedstr ' %F{9}•%f'
zstyle ':vcs_info:git:*' formats '→ %F{14}%b%f%m%u%c'
zstyle ':vcs_info:git:*' actionformats '→ %F{14}%b %F{13}%a%f'
# }}}

# {{{ prompt
setopt prompt_subst

PROMPT='%F{11}%n %F{12}%M %F{10}%~%f ${vcs_info_msg_0_}
%(!.#.») '

PS2='  » '

promptinit
# }}}
# {{{ completion
zstyle ':completion:*' completer _complete
zstyle ':completion:*' matcher-list '' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' '+l:|=* r:|=*'
compinit
# }}}

# {{{ functions
mosh() {
  case "$1" in
    (akita|chiba|yamagata|gunma|tokyo|inaba|odroid) )
      command mosh "$1" -- tmuxinator start master
      ;;
    *)
      command mosh "$@"
  esac
}

fzf-git-commit() {
  local commits ret
  setopt localoptions noglobsubst noposixbuiltins pipefail 2>/dev/null
  commits=($(git log --oneline \
    | FZF_DEFAULT_OPTS="--height 40% $FZF_DEFAULT_OPTS --tiebreak=index" fzf \
        -m --preview 'git show --color=always --raw $(cut -d" " -f1 <<< {})' \
    | cut -d' ' -f1)
  )
  ret=$?
  LBUFFER="${LBUFFER}${commits[@]}"
  zle reset-prompt
  return $ret
}

fzf-git-branch() {
  local commits ret
  setopt localoptions noglobsubst noposixbuiltins pipefail 2>/dev/null
  branches=($(git for-each-ref --format='%(refname:short)' refs \
    | FZF_DEFAULT_OPTS="--height 40% $FZF_DEFAULT_OPTS --tiebreak=index" fzf \
        -m --preview 'git log --graph --color=always --oneline {}' )
  )
  ret=$?
  LBUFFER="${LBUFFER}${branches[@]}"
  zle reset-prompt
  return $ret
}
# }}}
# {{{ aliases
alias monad=xmonad
alias ls='ls --color=auto'
alias nc='nvim ~/.config/nvim/init.vim'
alias arec='ffmpeg -f pulse -i default -c:a copy'
alias kc='kubectl'
alias kx='kubectx'
alias ks='kubens'
alias p='xclip -selection c -o'
alias c='xclip -selection c -i'
# }}}
# {{{ keybinds
bindkey -e

bindkey '\e[A' history-beginning-search-backward
bindkey '\e[B' history-beginning-search-forward

bindkey '\e[1~' beginning-of-line
bindkey '\e[4~' end-of-line
bindkey '\e[5~' beginning-of-history
bindkey '\e[3~' delete-char
bindkey '\e[2~' quoted-insert
bindkey '\eu' backward-kill-line
bindkey '\ew' backward-kill-word
bindkey '^[^[[D' backward-word
bindkey '^[^[[C' forward-word

zle     -N   fzf-git-commit
bindkey '^F' fzf-git-commit
zle     -N   fzf-git-branch
bindkey '^K' fzf-git-branch
# }}}

# {{{ xdg
export XDG_DESKTOP_DIR="$HOME"
export XDG_DOCUMENTS_DIR="$HOME/documents"
export XDG_DOWNLOAD_DIR="$HOME/downloads"
export XDG_MUSIC_DIR="$HOME/music"
export XDG_PICTURES_DIR="$HOME/pictures"
export XDG_VIDEOS_DIR="$HOME/videos"
export XDG_CONFIG_HOME="$HOME/.config"
export XDG_CACHE_HOME="$HOME/.cache"
export XDG_RUNTIME_DIR="/run/user/$UID"
export XDG_DATA_HOME="$HOME/.local/share"
# }}}
# {{{ env
export NO_AT_BRIDGE=1
export EDITOR=nvim
export BROWSER=firefox

export GPG_TTY=$(tty)

export CC=/usr/bin/gcc
export CXX=/usr/bin/g++

export MAKEFLAGS=-j5
export CFLAGS=" -O3 -march=native "
export CXXFLAGS=" -O3 -march=native "

export LESSHISTFILE=-
export LESS="-R -S -# 4"
export LESSOPEN="|lesspipe.sh %s"

export GOPATH=~/.go

export _JAVA_AWT_WM_NONREPARENTING=1
export _JAVA_OPTIONS='-Dawt.useSystemAAFontSettings=lcd -Dswing.aatext=true'

export MOSH_ESCAPE_KEY='~'
export LIBVA_DRIVER_NAME=i965

export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=7'
# }}}

path+=(
  "$HOME/.local/bin"
  "$HOME/.cabal/bin"
  "$HOME/.go/bin"
  "$HOME/.gem/ruby/2.6.0/bin"
  "$HOME/.gem/ruby/2.7.0/bin"
)

export PATH

source /usr/share/fzf/completion.zsh
source /usr/share/fzf/key-bindings.zsh
source /usr/lib/ruby/gems/3.0.0/gems/tmuxinator-*/completion/tmuxinator.zsh
source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

autoload -U +X bashcompinit && bashcompinit
complete -o nospace -C /usr/bin/terraform terraform

# vim: set et ts=2 sw=2 :
